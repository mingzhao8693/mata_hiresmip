;====================================================================================
; ncl 'mode="NAM"' 'season="ALL"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="DJF"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="JJA"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="MAM"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="SON"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="NDJFM"'  flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="MJJAS"'  flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="APR"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="OCT"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="NDJFMA"' flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="MJJASO"' flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="ALL"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="DJF"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="JJA"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="MAM"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="SON"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="NDJFM"'  flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="MJJAS"'  flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="APR"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="OCT"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="NDJFMA"' flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="NAM"' 'season="MJJASO"' flip=True   'expn="ERA_interim"'   mizeof.ncl
;====================================================================================
; ncl 'mode="SAM"' 'season="ALL"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="DJF"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="JJA"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="MAM"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="SON"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="NDJFM"'  flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="MJJAS"'  flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="APR"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="OCT"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="NDJFMA"' flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="MJJASO"' flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="ALL"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="DJF"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="JJA"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="MAM"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="SON"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="NDJFM"'  flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="MJJAS"'  flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="APR"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="OCT"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="NDJFMA"' flip=False  'expn="ERA_interim"'   mizeof.ncl
; ncl 'mode="SAM"' 'season="MJJASO"' flip=True   'expn="ERA_interim"'   mizeof.ncl
;====================================================================================
;====================================================================================
====================================================================================
 ncl 'mode="PNA"' 'season="ALL"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="DJF"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="JJA"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="MAM"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="SON"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="NDJFM"'  flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="MJJAS"'  flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="APR"'    flip=True   'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="OCT"'    flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="NDJFMA"' flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="MJJASO"' flip=False  'expn="c192L33_am4p0_amip_HIRESMIP_H8"'   mizeof.ncl

 ncl 'mode="PNA"' 'season="ALL"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="DJF"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="JJA"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="MAM"'    flip=False  'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="SON"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="NDJFM"'  flip=False  'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="MJJAS"'  flip=True   'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="APR"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="OCT"'    flip=True   'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="NDJFMA"' flip=True   'expn="ERA_interim"'   mizeof.ncl
 ncl 'mode="PNA"' 'season="MJJASO"' flip=False  'expn="ERA_interim"'   mizeof.ncl
;====================================================================================
;====================================================================================
; eof_6_640.ncl
;
; Concepts illustrated:
;   - Calculating EOFs
;   - Drawing a black-and-white XY plot
;   - Applying spatial weighting
;   - Writing EOFs to a NetCDF file
;   - Labeling the X axis with nicely-formatted time labels
;   - Drawing a time series plot
;================================================
; This script uses functions eofunc_n_Wrap and 
; eofunc_ts_n_Wrap, which were added in NCL V6.4.0.
;================================================
; These files are loaded by default in NCL V6.2.0 and newer
; load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
; load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
; load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"

  ;mode  ="PNA"
  ;season="DJF"
  ;flip  = True
  ;expn  ="c192L33_am4p0_amip_HIRESMIP_H8"
  
;*******************************************
; Read data
;*******************************************
  if (mode .eq. "NAM")
    lat1 = 20
    lat2 = 90
    lon1 = 0
    lon2 = 360
    region ="_20N-90N_"
  elseif (mode .eq. "SAM")
    lat1 = -20
    lat2 = -90
    lon1 = 0
    lon2 = 360
    region ="_20S-90S_"
  elseif (mode .eq. "PNA")
    lat1 =  0
    lat2 =  90
    lon1 = 120
    lon2 = 360
    region ="_0N-90N_120E-360E_"
  end if	

  if (expn .eq. "ERA_interim")
    diri = "/archive/Ming.Zhao/obs_for_am4p0_paper/ERA_interim/monthly/"
  elseif (expn .eq. "c192L33_am4p0_amip_HIRESMIP_H8")
    diri = "/archive/Ming.Zhao/awg/warsaw_201710/"
  end if

  if (mode .eq. "PNA")
   varname="Z500"
   if (expn .eq. "ERA_interim")
    fili   = "z.197901-201512.nc"                        ; rectilinear
    fn     = diri+fili
    f      = addfile(fn,"r")                             ; open file                 
    varn   = f->z({692496:1007328},21,{lat1:lat2},{lon1:lon2})  ; p(time,latitude,longtitude)       
    varn   = varn/9.8;
   elseif (expn .eq. "c192L33_am4p0_amip_HIRESMIP_H8")
    fili   = "/ts_all/atmos.194901-201412.hght.nc"   ; rectilinear
    fn     = diri+expn+fili
    f      = addfile(fn,"r")                         ; open file                 
    varn   = f->hght({10972.5:24090.5},6,{lat1:lat2},{lon1:lon2}); 1979-2014;p(time,latitude,longtitude)       
    ;varn   =doubletofloat(varn)
   end if
  else
   varname="SLP"
   if (expn .eq. "ERA_interim")
    fili   = "msl.197901-201512.nc"                  
    fn     = diri+fili
    f      = addfile(fn,"r")                         ; open file                 
    varn   = f->msl({692496:1007328},{lat1:lat2},:)  ; 1979-2014;varn(time,lat,lon)
    varn   = varn*0.01;
   elseif (expn .eq. "c192L33_am4p0_amip_HIRESMIP_H8")
    fili   = "/ts_all/atmos.197901-201412.slp.nc"    
    fn     = diri+expn+fili
    f      = addfile(fn,"r")                         ; open file                 
    varn   = f->slp({10972.5:24090.5},{lat1:lat2},:) ; 1979-2014;varn(time,lat,lon)
   end if
  end if

  printVarSummary(varn)                          
  period ="_1979_2014"
  varnam ="_"+varname+region

  varn_clm = clmMonTLL(varn)
  varn_ano = calcMonAnomTLL(varn,varn_clm)
  printVarSummary(varn_clm)                            
  printVarSummary(varn_ano)    
  
  ;season = "DJF" 
  if (season .eq. "DJF") then
    t1=ispan(0,  1,1)
    t2=ispan(11,13,1)
  elseif (season .eq. "JJA") then
    t1=ispan(5,7,1)
    t2=t1+12
  elseif (season .eq. "MAM") then
    t1=ispan(2,4,1)
    t2=t1+12
  elseif (season .eq. "SON") then
    t1=ispan(8,10,1)
    t2=t1+12
  elseif (season .eq. "NDJFM") then
    t1=ispan(0,2,1)
    t2=ispan(10,14,1)
  elseif (season .eq. "MJJAS") then
    t1=ispan(4,8,1)
    t2=t1+12
  elseif (season .eq. "APR") then
    t1=ispan(3,3,1)
    t2=t1+12
  elseif (season .eq. "OCT") then
    t1=ispan(9,9,1)
    t2=t1+12
  elseif (season .eq. "NDJFMA") then
    t1=ispan(0,3,1)
    t2=ispan(10,15,1)
  elseif (season .eq. "MJJASO") then
    t1=ispan(4,9,1)
    t2=t1+12
  else
    t1=ispan(0,11,1)
    t2=t1+12
  end if
  tidx0 = array_append_record(t1,t2,0)
  ;print(tidx0)
  i = 0
  do while(i.le.33)
    t2 = t2+12
    i=i+1
    tidx = array_append_record(tidx0,t2,0)
    delete(tidx0)
    tidx0 = tidx
    delete(tidx)
  end do
  tidx=tidx0
  delete(tidx0)
  if (season .eq. "DJF") then
    delete(t2)
    t2=ispan(431,431,1)
    tidx0=array_append_record(tidx,t2,0)
    delete(tidx);
    tidx=tidx0;
  elseif (season .eq. "NDJFM" .or. season .eq. "NDJFMA") then
    delete(t2)
    t2=ispan(430,431,1)
    tidx0=array_append_record(tidx,t2,0)
    delete(tidx);
    tidx=tidx0;
  end if
  ; print(tidx)

;  varnx = varn(0,:,:)
;  wks = gsn_open_wks("x11","plot SLP")  ; output to screen
;  wks = gsn_open_wks("png","ncl_slp")   ; output to file ncl_slp.png    
;  res              = True               ; plot mods desired
;  res@tiMainString = "Default Color"    ; main title
;  res@cnFillOn     =  True              ; turn on color fill
;  res@tiMainString = "NCL plot of SLP"
;  plot = gsn_csm_contour_map(wks, varnx, res)     ; create plot
;  quit

  p = varn_ano(tidx,:,:)                              ; do EOF for varn_ano
;*******************************************
; Calculate EOFs
;*******************************************
  if (expn .eq. "ERA_interim")
    w = sqrt(cos(0.01745329*p&latitude))
  else
    w = sqrt(cos(0.01745329*p&lat))
  end if
  wp = p*conform(p, w, 1)                             ; wp(21,61,240)
  copy_VarCoords(p, wp)                               ; wp(time,lat,lon)
  printVarSummary(wp)                                 ; [time | 21] x [latitude | 61] x [longitude | 240]
  
  neof   = 4                                          ; user specified (commonly <=5) 
  eof    = eofunc_n_Wrap(wp, neof, False, 0)
  eof_ts = eofunc_ts_n_Wrap (wp, eof, False, 0)
  
  printVarSummary( eof )                              ; examine EOF variables                 
  printVarSummary( eof_ts )

  if (flip .eq. True)
    eof   =-eof
    eof_ts=-eof_ts
  end if

;*******************************************
; Create netCDF: simple approach                                               
;*******************************************
  fnc          = expn+period+varnam+season+"_EOF.nc"
  system("/bin/rm -f "+fnc)               ; rm any pre-existing file            
  fout         = addfile(fnc, "c")        ; new netCDF file                   
  fout@title   = "EOF of "+varname+": 1979-2014"
  fout->EOF    = eof
  fout->EOF_TS = eof_ts
  fout->tim_idx= tidx
  
;*******************************************
; North significance test: any pcvar, eval_transpose, eval can be used
;*******************************************
  print("--- eofunc_north ---") 
  dimp   = dimsizes(p)
  ntim   = dimp(0)                         ; max # eigenvalues possible  
  prinfo = True
  sig    = eofunc_north(eof@pcvar, ntim, prinfo)                                                              
  print("---")
;*******************************************
;  Create a 'time' axis for plot
;*******************************************
  yyyymm = cd_calendar(eof_ts&time,-1)  
  yrfrac = yyyymm_to_yyyyfrac(yyyymm, 0.0) ; not used here

;*******************************************
;  plots
;*******************************************

  pltType = "png"                        ; send graphics to PNG file
  wks  = gsn_open_wks(pltType,expn+period+varnam+season+"_EOF")
  plot = new(neof,graphic)               ; create graphic array
                                         ; only needed if paneling
  res                     = True         
  res@gsnDraw             = False        ; don't draw yet
  res@gsnFrame            = False        ; don't advance frame yet

  if (mode .eq. "NAM" )
    res@gsnPolar          = "NH"         ; specify the hemisphere
    res@mpCenterLonF      = -90.         ; default is 0 [GM]
  elseif(mode .eq. "SAM")
    res@gsnPolar          = "SH"         ; specify the hemisphere
    res@mpCenterLonF      =  0.          ; default is 0 [GM]
  else
    res@mpProjection      = "LambertConformal" ; choose projection
  end if

  if (expn .eq. "ERA_interim")
    res@mpMinLatF         = min(wp&latitude)
    res@mpMaxLatF         = max(wp&latitude)
  else
    res@mpMinLatF         = min(doubletofloat(wp&lat)) ; this doubletofloat is important
    res@mpMaxLatF         = max(doubletofloat(wp&lat)) ; this doubletofloat is important
  end if

  res@cnFillOn            = True         ; turn on color fill
  res@cnFillPalette       = "BlWhRe"     ; choose colormap
  res@cnLinesOn           = True         ; True is default
  res@cnLineLabelsOn      = False        ; True is default
  res@lbLabelBarOn        = False        ; turn off individual lb's
  symMinMaxPlt(eof, 16, False, res)      ; set symmetric plot min/max contributed.ncl
  ; panel plot only resources
  resP                    = True         ; modify the panel plot
  resP@gsnMaximize        = True         ; large format
  resP@gsnPanelLabelBar   = True         ; add common colorbar
  resP@gsnPanelMainString = expn+period+varnam+season+" ("+varname+" 1979-2014: eofunc_north)"
  if (mode .eq. "NAM" .or. mode .eq. "SAM")
    do n=0,neof-1
      res@gsnLeftString  = "EOF "+(n+1)
      res@gsnCenterString= "NORTH="+sig(n)
      res@gsnRightString = sprintf("%5.1f", eof@pcvar(n)) +"%"
      plot(n) = gsn_csm_contour_map_polar(wks,eof(n,:,:),res)
    end do
    gsn_panel(wks,plot,(/2,2/),resP)     ; draw all 'neof' as one plot
  else
    ; masked plot
    res@gsnAddCyclic          = False    ; regional plot
    res@mpMinLonF             = lon1     ; min lon to mask
    res@mpMaxLonF             = lon2     ; max lon to mask
    res@gsnMaskLambertConformal = True   ; turn on lc masking
    do n=0,neof-1
      res@gsnLeftString   = "EOF "+(n+1)
      res@gsnCenterString = "NORTH="+sig(n)
      res@gsnRightString  = sprintf("%5.1f", eof@pcvar(n)) +"%"
      plot(n) = gsn_csm_contour_map(wks,eof(n,:,:),res); create plot
    end do
    gsn_panel(wks,plot,(/2,2/),resP)     ; draw all 'neof' as one plot
  end if
;*******************************************
; time series (principal component) plot
;*******************************************
  eof_ts@long_name = "Amplitude"
  rts           = True
  rts@gsnDraw   = False              ; don't draw yet
  rts@gsnFrame  = False              ; don't advance frame yet
  rts@vpHeightF = 0.40               ; Changes the aspect ratio
  rts@vpWidthF  = 0.85
  rts@vpXF      = 0.10               ; change start locations
  rts@vpYF      = 0.75               ; the plot
  rts@gsnYRefLine           = 0.     ; reference line   
  rts@gsnAboveYRefLineColor = "red"  ; above ref line fill red
  rts@gsnBelowYRefLineColor = "blue" ; below ref line fill blue
; panel plot only resources
  rtsP                    = True   ; modify the panel plot
  rtsP@gsnMaximize        = True   ; large format
  rtsP@gsnPanelMainString = expn+period+varnam+season+" ("+varname+": 1979-2014: eofunc_north)"
  do n=0,neof-1
     rts@gsnLeftString  = "EOF "+(n+1)
     rts@gsnCenterString= "NORTH="+sig(n)
     rts@gsnRightString = sprintf("%5.1f", eof@pcvar(n)) +"%"
     plot(n) = gsn_csm_xy (wks,yrfrac,eof_ts(n,:),rts)
  end do
  gsn_panel(wks,plot,(/2,2/),rtsP) ; draw all 'neof' as one plot


